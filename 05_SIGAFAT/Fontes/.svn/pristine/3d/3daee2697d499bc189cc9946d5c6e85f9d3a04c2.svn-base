#Include 'Protheus.ch'

Static _cPedido     := ''
Static _cXItemP     := ''
Static _cObsEng     := ''
Static _cObsCom     := ''
Static _cEtapa      := ''
Static _cCliente    := ''
Static _cItem       := ''
Static _nQtdVen     := 0
Static _cEtapaAtu   := ''
Static _nRecSC5     := 0

//+------------------------------------------------------------------------------------------------------------------------
//  Apontamento de Etapa Tecnico Comercial
//+------------------------------------------------------------------------------------------------------------------------
User Function M05A21()
    Local _oDlg         := Nil
    Local _cTitle       := 'Apontamento Tec. Comercial.'
    Local _oEtapa       := Nil
    Local _oEtapaAtu    := Nil
    Local _aEtapas      := {'','B - Medição','C - Ag. Inform','D - Cancelado','E - Tec Com Lib'}
    Local _oObsEng      := Nil
    Local _oPedido      := Nil
    Local _oItem        := Nil
    Local _oXItemP      := Nil
    Local _oQtdVen      := Nil
    Local _oCliente     := Nil
    Local _bConf        := {||( M05AGetObs())}
    Local _oScr1        := Nil
    Local _nRadio		:= 0
    Local _aSize        := FWGetDialogSize(oMainWnd)

    MA05ClrVar(.T.)

    Define MsDialog _oDlg Title _cTitle Style DS_MODALFRAME  From _aSize[1],_aSize[2] To _aSize[3],_aSize[4] Pixel
        @015,020 Say  'Pedido:' Of _oDlg Pixel
        @012,090 Get _oPedido Var _cPedido Size 100,011 Of _oDlg Pixel Valid (Empty(_cPedido) .Or. ExistCpo('SC5',_cPedido))

        _oPedido:bLostFocus := _bConf

        @032,020 Say  'Sequência:' Of _oDlg Pixel
        @029,090 Get _oItem Var _cItem Size 100,010 Of _oDlg Pixel Valid (Empty(_cItem) .Or. Empty(_cPedido) .Or. ExistCpo('SC6',_cPedido + _cItem))

        _oItem:bLostFocus := _bConf

        @045,020 Say  'Etapa:' Of _oDlg Pixel
        _oEtapa := TComboBox():New(045,090,{|u|if(PCount()>0,_cEtapa:=u,_cEtapa)},_aEtapas,100,40,_oDlg,,,,,,.T.,,,,,,,,,'_cEtapa')

        @075,020 Say 'Etapa Atual:' Of _oDlg Pixel
        @072,090 Get _oEtapaAtu Var _cEtapaAtu Size 300,010 Of _oDlg Pixel WHEN .F.

        @090,020 Say  'Cliente:'  Of _oDlg Pixel
        @087,090 Get _oCliente Var _cCliente Size 300,010 Of _oDlg Pixel WHEN .F.

        @105,020 Say  'Item:'  Of _oDlg Pixel
        @102,090 Get _oXItemP Var _cXItemP Size 100,010 Of _oDlg Pixel WHEN .F.

        @120,020 Say  'Quantidade'  Of _oDlg Pixel
        @117,090 Get _oQtdVen Var Transform(_nQtdVen,"@E 999,999.9999")    Size 100,010 Of _oDlg Pixel WHEN .F.

        @140,020 Say  'Descr. Pedido:' Of _oDlg Pixel
        _oScr1 := TScrollBox():New(_oDlg,140,090,060,300,.T.,.T.,.T.)
        @005,005 Say {||_cObsCom} Size 280,060 Of _oScr1 Pixel

        @210,020 Say  'Obs. Engenharia:' Of _oDlg Pixel
        @210,090 Get _oObsEng Var _cObsEng Memo Size 300,060 Of _oDlg Pixel

        @015,550 BUTTON "Confirmar"     SIZE 040, 015 PIXEL OF _oDlg ACTION (IIF(!Empty(_cPedido),(M05AOk()), ))
        @035,550 BUTTON "Cancelar"      SIZE 040, 015 PIXEL OF _oDlg ACTION (_oDlg:End())
        
        @055,550 BUTTON "Anexar"        SIZE 040, 015 PIXEL OF _oDlg ACTION (MA05Anexar())
        @075,550 BUTTON "Vis. Anexo"    SIZE 040, 015 PIXEL OF _oDlg ACTION (M05AVisAne())
        
		@095,550 BUTTON "Estornar"		SIZE 040, 015 PIXEL OF _oDlg ACTION (M05AEST(_cPedido,_cItem))
        
		//@ 125, 550 RADIO oRadio VAR _nRadio 3D SIZE 70, 11 PROMPT "Teste1","Teste2","Teste3" of _oDlg PIXEL 

    Activate MsDialog _oDlg  Centered
Return


//+---------------------------------------------------------------------------------
Static Function MA05Anexar()
    Local _cMascara     := 'Todos os arquivos|*.*'
    Local _cTitulo      := 'Escolha o arquivo'
    Local _nMascpad     := 0
    Local _cDirOri      := 'C:\'
    Local _cDirDes      := AllTrim(GetMv('AM_05A21_A',.T.,''))
    Local _lSalvar      := .F. /*.F. = Salva || .T. = Abre*/
    Local _nOpcoes      := GETF_LOCALHARD
    Local _lArvore      := .F. /*.T. = apresenta o árvore do servidor || .F. = não apresenta*/
    Local _aArquivo     := {}
    Local _lOk          := .F.

    If !Empty(_cPedido) .And. !Empty(_cItem)
        SC6->(DbSetOrder(1))
        SC6->(DbGoTop())

        If SC6->(DbSeek(xFilial('SC6') + _cPedido + _cItem))
            If Empty(_cDirDes)
                MsgInfo(I18N('É obrigatório informar o diretório de armazenamento no parâmetro AM_05A21_A.'))
            Else
                _cDirDes    += AllTrim( SC6->C6_FILIAL + SC6->C6_NUM +  SC6->C6_ITEM + SC6->C6_PRODUTO) + '\'
                _cDirOri    := cGetFile( _cMascara, _cTitulo, _nMascpad, _cDirOri, _lSalvar, _nOpcoes, _lArvore)

                If !Empty(_cDirOri)
                    _aFiles := Directory(_cDirOri, "D")

                    If !(_lOk := !File(_cDirDes + _aFiles[1][1]))
                        If MsgYesNo('Arquivo já anexado para esta sequência do pedido de venda.' + CRLF + 'Deseja atualizar?')
                            If !(_lOk := FErase(_cDirDes + _aFiles[1][1]) <> -1)
                                MsgInfo('Erro ao apagar arquivo: ' + STR(FERROR()))
                            EndIf
                        EndIf
                    EndIf

                    If _lOk
                        MakeDir(_cDirDes)
                        __CopyFile(_cDirOri,_cDirDes + _aFiles[1][1])

                        If File(_cDirDes + _aFiles[1][1])
                            MsgInfo('Arquivo anexado com sucesso!')
                        Else
                            MsgInfo('Erro ao anexar arquivo.')
                        EndIf
                    EndIf
                Else
                    MsgInfo('Arquivo não informado!')
                EndIf
            EndIf

        Else
            MsgInfo(I18N('Pedido #1 e sequência #2 não localizados para anexar arquivo.',{_cPedido,_cItem}))
        EndIf
    EndIf
Return

//+---------------------------------------------------------------------------------
Static Function M05AVisAne()
        Local _cDir     := AllTrim(GetMv('AM_05A21_A',.T.,''))

    If !Empty(_cPedido) .And. !Empty(_cItem)
        SC6->(DbSetOrder(1))
        SC6->(DbGoTop())

        If SC6->(DbSeek(xFilial('SC6') + _cPedido + _cItem))
            If Empty(_cDir)
                MsgInfo('É obrigatório informar o diretório de armazenamento no parâmetro AM_05A21_A.')
            Else
                _cDir   += AllTrim( SC6->C6_FILIAL + SC6->C6_NUM +  SC6->C6_ITEM + SC6->C6_PRODUTO) + '\'

                If ExistDir(_cDir)
                    WinExec('explorer.exe ' + _cDir)
                Else
                    MsgInfo(I18N('Não foram encontrados anexos para o pedido #1 e sequencia #2 .',{_cPedido,_cItem}))
                EndIf
            EndIf
        Else
            MsgInfo(I18N('Pedido #1 e sequência #2 não localizados para anexar arquivo.',{_cPedido,_cItem}))
        EndIf
    EndIf
Return

//+---------------------------------------------------------------------------------
Static Function M05AGetObs()

    If !Empty(_cPedido)
        SC5->(DbSetOrder(1))
        SC5->(DbGoTop())

        If SC5->(DbSeek(xFilial('SC5') + _cPedido))
            _cCliente   := AllTrim(Posicione('SA1',1,xFilial('SA1') + SC5->C5_CLIENTE + SC5->C5_LOJACLI,'A1_NOME'))
            _nRecSC5     := SC5->(Recno())

            If !SC5->(DBRLock(_nRecSC5))
                 MsgStop(I18N('Pedido #1 não pode ser alterado pois encontra-se em manutenção por outro usuário.',{_cPedido}))
                 MA05ClrVar(.T.)
            EndIf
        Else
            MsgStop(I18N('Pedido #1 não localizado.',{_cPedido}))
            MA05ClrVar(.T.)
        EndIf
    Else
        MA05ClrVar(.T.)
    EndIf

    If !Empty(_cItem)
        If !Empty(_cPedido)
            SC6->(DbSetOrder(1))
            SC6->(DbGoTop())

            If SC6->(DbSeek(xFilial('SC6') + _cPedido + _cItem))
                _cObsEng    := SC6->C6_XOBSENG
                _cObsCom    := Posicione('SB1',1,xFilial('SB1') + SC6->C6_PRODUTO, 'B1_DESC')

                _cXItemP    := SC6->C6_XITEMP
                _nQtdVen    := SC6->C6_QTDVEN
                _cEtapaAtu  := MA05GetEt()
            Else
                MsgStop(I18N('Pedido #1 e Sequencia #2 não localizados.',{_cPedido,_cItem}))
                MA05ClrVar(.F.)
            EndIf
        EndIf
    Else
        MA05ClrVar(.F.)
    EndIf
Return

//+---------------------------------------------------------------------------------]
Static Function MA05GetEt()
    Local _cRet := ''

    _cRet := Posicione("ZA3",1,XFILIAL("ZA3") + SC6->C6_XETAPA,"ZA3_DESCRI")

Return _cRet

//+---------------------------------------------------------------------------------
Static Function M05AOk()
    Local _cXEtapa  := ''
    Local _cCpData  := ''
    Local _cCpHora  := ''
    Local _cCpUser  := ''

    If M05AValid()
        SC6->(DbGoTop())
        SC6->(DbSetOrder(1))

        If SC6->(DbSeek(xFilial('SC6') + _cPedido + _cItem))
            Do Case
                Case SubStr(_cEtapa,1,1) == 'B'
                    _cXEtapa    := 'B'  // 'Medição'
                    _cCpData    := 'C6_XMEDDT'
                    _cCpHora    := 'C6_XMEDHR'
                    _cCpUser    := 'C6_XMEDUS'
                Case SubStr(_cEtapa,1,1) == 'C'
                    _cXEtapa    := 'C'  // 'Aguar. Informação'
                    _cCpData    := 'C6_XAINDT'
                    _cCpHora    := 'C6_XAINHR'
                    _cCpUser    := 'C6_XAINUS'
                Case SubStr(_cEtapa,1,1) == 'D'
                    _cXEtapa    := 'D'  // 'Cancelado'
                    _cCpData    := 'C6_XCANDT'
                    _cCpHora    := 'C6_XCANHR'
                    _cCpUser    := 'C6_XCANUS'
                Case SubStr(_cEtapa,1,1) == 'E'
                    _cXEtapa    := 'E'  // 'Lib Dep Tec Com
                    _cCpData    := 'C6_XGOPDT'
                    _cCpHora    := 'C6_XGOPHR'
                    _cCpUser    := 'C6_XGOPUS'
            EndCase

            If !Empty(_cEtapa)
                RecLock('SC6',.F.)
                    SC6->C6_XETAPA  	:= _cXEtapa
                    SC6->&(_cCpData)    := dDataBase
                    SC6->&(_cCpHora)    := Time()
                    SC6->&(_cCpUser)    := Upper(UsrRetName(RetCodUsr()))
                    SC6->C6_XOBSENG 	:= _cObsEng
                SC6->(MsUnLock())

                MsgInfo('Etapa apontada com sucesso!','OK')

                MA05ClrVar(.F.)
            EndIf
        Else
            MsgStop(I18N('Pedido #1 e Sequencia #2 não localizados.',{_cPedido,_cItem}))
        EndIf
    EndIf
Return

//+---------------------------------------------------------------------------------
Static Function MA05ClrVar(_lPV)
    Default _lPV := .T.

    If _lPV
        _cPedido         := Space(TamSX3('C6_NUM')[1])
        _cEtapa          := ''
        _cCliente        := ''

        If _nRecSC5 > 0
              SC5->(DBRUnlock(_nRecSC5))
        EndIf

        _nRecSC5      := 0
    EndIf

    _cItem      := Space(TamSX3('C6_ITEM')[1])
    _cObsEng    := ''
    _cObsCom    := ''
    _cXItemP    := ''
    _nQtdVen    := ''
    _cEtapaAtu  := ''
Return

//+---------------------------------------------------------------------------------
Static Function M05AValid()
    Local _lRet             := .F.
    Local _cPedCancel       := AllTrim(GetMv('AM_05A21_B',.T.,''))
    Local _cAtuDes          := ''

    If !(_lRet := !Empty(_cPedido) .And. !Empty(_cItem) .And. !Empty(_cEtapa))
        MsgInfo('É obrigatório informar Pedido, Sequência e Etapa.' + CRLF + 'Verifique.')
    Else
        If SubStr(_cEtapa,1,1) == 'D' // Cancelado
            If !(_lRet := AllTrim(_cPedido + _cItem) == _cPedCancel)
                MsgInfo('Para cancelamento é necessário autorização prévia da Diretoria.')
            EndIf
        EndIf
    EndIf

    // Valida ordem de etapa
    If _lRet
        _lRet := U_M05A22(SubStr(_cEtapa,1,1) ,_cPedido,_cItem,@_cAtuDes)

        If ! _lRet
            MsgInfo(I18N('Apontamento não permitido.' + CRLF + CRLF + 'Motivo: Item já se encontra na etapa #1 .',{AllTrim(_cAtuDes)}))
        EndIf
    EndIf

Return _lRet
//+---------------------------------------------------------------------------------
Static Function M05AEST(_cPedido,_cItem)
	_nOpc	:= 0

	dbSelectArea("SC2")
	dbSetOrder(1)

	If SC2->(MsSeek(xFilial("SC2")+_cPedido+_cItem))
		MsgStop( "Já existe ordem de produção gerada para o Pedido / Item !" , "ATENÇÃO !!" )
	Else
		Do Case
			Case Empty(_cPedido)
				MsgAlert( "Informe o numero do pedido!" , "ATENÇÃO !!" )
			Case Empty(_cItem)
				MsgAlert( "Informe o item do pedido!" , "ATENÇÃO !!" )
			Case Empty(_cEtapa) .And. _cEtapaAtu == "Tec Com Lib "
				RecLock('SC6',.F.)
				SC6->C6_XETAPA		:= " "
				SC6->C6_XMEDDT		:= dDataBase
				SC6->C6_XMEDHR		:= Time()
				SC6->C6_XMEDUS		:= Upper(UsrRetName(RetCodUsr()))
				SC6->(MsUnLock())
				_cEtapaAtu := " "
				MsgInfo( "Estorno efetuado com sucesso !" , "SUCESSO" )
			Case _cEtapaAtu <> "Tec Com Lib "
				MSGALERT( "Só é possível efetuar o estorno da etapa (Tec.Com.Lib.)" , "ATENÇÃO !!" )
		EndCase
	Endif

Return
